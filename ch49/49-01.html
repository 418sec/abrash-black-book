<HTML>
<HEAD>
<META name=vsisbn content="1576101746">
<META name=vstitle content="Michael Abrash's Graphics Programming Black Book, Special Edition">
<META name=vsauthor content="Michael Abrash">
<META name=vspublisher content="The Coriolis Group">
<META name=vspubdate content="07/01/97">
<META name=vscategory content="Web and Software Development: Game Development,Web and Software Development: Graphics and Multimedia Development">






<TITLE>Michael Abrash's Graphics Programming Black Book Special Edition: Mode X 256-Color Animation</TITLE>

<!-- HEADER -->
<!-- Empty Reference Subhead -->

<!--ISBN=1576101746//-->
<!--TITLE=Michael Abrash's Graphics Programming Black Book Special Edition//-->
<!--AUTHOR=Michael Abrash//-->
<!--PUBLISHER=The Coriolis Group, Inc.//-->
<!--CHAPTER=49//-->
<!--PAGES=913-918//-->
<!--UNASSIGNED1//-->
<!--UNASSIGNED2//--></HEAD><BODY LINK=#0000FF ALINK=#000099 VLINK=#0000FF BGCOLOR=#FFFFFF>

<CENTER>
<TABLE BORDER>
<TR>
<TD><A HREF="../ch48/48-05.html">Previous</A></TD>
<TD><A HREF="../index.html">Table of Contents</A></TD>
<TD><A HREF="49-02.html">Next</A></TD>
</TR>
</TABLE>
</CENTER>
<P><BR></P>
<H2><A NAME="Heading1"></A><FONT COLOR="#000077">Chapter 49<BR>Mode X 256-Color Animation
</FONT></H2>
<H3><A NAME="Heading2"></A><FONT COLOR="#000077">How to Make the VGA Really Get up and Dance</FONT></H3>
<P>Okay&#151;no amusing stories or informative anecdotes to kick off this chapter; lotta ground to cover, gotta hurry&#151;you&#146;re impatient, I can smell it. I won&#146;t talk about the time a friend made the mistake of loudly saying &#147;$100 bill&#148; during an animated discussion while walking among the bums on Market Street in San Francisco one night, thereby graphically illustrating that context is everything. I can&#146;t spare a word about how my daughter thinks my 11-year-old floppy-disk-based CP/M machine is more powerful than my 386 with its 100-MB hard disk because the CP/M machine&#146;s word processor loads and runs twice as fast as the 386&#146;s Windows-based word processor, demonstrating that progress is not the neat exponential curve we&#146;d like to think it is, and that features and performance are often conflicting notions. And, lord knows, I can&#146;t take the time to discuss the habits of small white dogs, notwithstanding that such dogs seem to be relevant to just about every aspect of computing, as Jeff Duntemann&#146;s writings make manifest. No lighthearted fluff for us; we have real work to do, for today we animate with 256 colors in Mode X.
</P>
<H3><A NAME="Heading3"></A><FONT COLOR="#000077">Masked Copying</FONT></H3>
<P>Over the past two chapters, we&#146;ve put together most of the tools needed to implement animation in the VGA&#146;s undocumented 320&#215;240 256-color Mode X. We now have mode set code, solid and 4&#215;4 pattern fills, system memory-to-display memory block copies, and display memory-to-display memory block copies. The final piece of the puzzle is the ability to copy a nonrectangular image to display memory. I call this <I>masked copying</I>.</P>
<P>Masked copying is sort of like drawing through a stencil, in that only certain pixels within the destination rectangle are drawn. The objective is to fit the image seamlessly into the background, without the rectangular fringe that results when nonrectangular images are drawn by block copying their bounding rectangle. This is accomplished by using a second rectangular bitmap, separate from the image but corresponding to it on a pixel-by-pixel basis, to control which destination pixels are set from the source and which are left unchanged. With a masked copy, only those pixels properly belonging to an image are drawn, and the image fits perfectly into the background, with no rectangular border. In fact, masked copying even makes it possible to have transparent areas within images.</P>
<P>Note that another way to achieve this effect is to implement copying code that supports a transparent color; that is, a color that doesn&#146;t get copied but rather leaves the destination unchanged. Transparent copying makes for more compact images, because no separate mask is needed, and is generally faster in a software-only implementation. However, Mode X supports masked copying but not transparent copying in hardware, so we&#146;ll use masked copying in this chapter.</P>
<P>The system memory to display memory masked copy routine in Listing 49.1 implements masked copying in a straightforward fashion. In the main drawing loop, the corresponding mask byte is consulted as each image pixel is encountered, and the image pixel is copied only if the mask byte is nonzero. As with most of the system-to-display code I&#146;ve presented, Listing 49.1 is not heavily optimized, because it&#146;s inherently slow; there&#146;s a better way to go when performance matters, and that&#146;s to use the VGA&#146;s hardware.</P>
<P><B>LISTING 49.1 L49-1.ASM</B></P>
<!-- CODE //-->
<PRE>
; Mode X (320x240, 256 colors) system memory-to-display memory masked copy
; routine. Not particularly fast; images for which performance is critical
; should be stored in off-screen memory and copied to screen via latches. Works
; on all VGAs. Copies up to but not including column at SourceEndX and row at
; SourceEndY. No clipping is performed. Mask and source image are both byte-
; per-pixel, and must be of same widths and reside at same coordinates in their
; respective bitmaps. Assembly code tested with TASM C near-callable as:
;
;    void CopySystemToScreenMaskedX(int SourceStartX,
;       int SourceStartY, int SourceEndX, int SourceEndY,
;       int DestStartX, int DestStartY, char * SourcePtr,
;       unsigned int DestPageBase, int SourceBitmapWidth,
;       int DestBitmapWidth, char * MaskPtr);

SC_INDEX equ    03c4h   ;Sequence Controller Index register port
MAP_MASK equ    02h     ;index in SC of Map Mask register
SCREEN_SEG equ  0a000h  ;segment of display memory in mode X

parms   struc
        dw      2 dup (?) ;pushed BP and return address
SourceStartX dw ?         ;X coordinate of upper left corner of source
                          ; (source is in system memory)
SourceStartY dw ?         ;Y coordinate of upper left corner of source
SourceEndX   dw ?         ;X coordinate of lower right corner of source
                          ; (the column at EndX is not copied)
SourceEndY   dw ?         ;Y coordinate of lower right corner of source
                          ; (the row at EndY is not copied)
DestStartX   dw ?         ;X coordinate of upper left corner of dest
                          ; (destination is in display memory)
DestStartY   dw ?         ;Y coordinate of upper left corner of dest
SourcePtr    dw ?         ;pointer in DS to start of bitmap which source resides
DestPageBase dw ?         ;base offset in display memory of page in
                          ; which dest resides
SourceBitmapWidth   dw ?  ;# of pixels across source bitmap (also must
                          ; be width across the mask)
DestBitmapWidth     dw ?  ;# of pixels across dest bitmap (must be multiple of 4)
MaskPtr             dw ?  ;pointer in DS to start of bitmap in which mask
                          ; resides (byte-per-pixel format, just like the source
                          ; image; 0-bytes mean don&#146;t copy corresponding source
                          ; pixel, 1-bytes mean do copy)
parms   ends

RectWidth equ   -2      ;local storage for width of rectangle
RectHeight equ  -4      ;local storage for height of rectangle
LeftMask equ    -6      ;local storage for left rect edge plane mask
STACK_FRAME_SIZE equ 6
        .model  small
        .code
        public  _CopySystemToScreenMaskedX
_CopySystemToScreenMaskedX proc    near
        push    bp                       ;preserve caller&#146;s stack frame
        mov     bp,sp                    ;point to local stack frame
        sub     sp,STACK_FRAME_SIZE      ;allocate space for local vars
        push    si                       ;preserve caller&#146;s register variables
        push    di

        mov     ax,SCREEN_SEG            ;point ES to display memory
        mov     es,ax
        mov     ax,[bp&#43;SourceBitmapWidth]
        mul     [bp&#43;SourceStartY]    ;top source rect scan line
        add     ax,[bp&#43;SourceStartX]
        mov     bx,ax
        add     ax,[bp&#43;SourcePtr]    ;offset of first source rect pixel
        mov     si,ax                    ; in DS
        add     bx,[bp&#43;MaskPtr]      ;offset of first mask pixel in DS

        mov     ax,[bp&#43;DestBitmapWidth]
        shr     ax,1                     ;convert to width in addresses
        shr     ax,1
        mov     [bp&#43;DestBitmapWidth],ax ;remember address width
        mul     [bp&#43;DestStartY]         ;top dest rect scan line
        mov     di,[bp&#43;DestStartX]
        mov     cx,di
        shr     di,1                        ;X/4 = offset of first dest rect pixel in
        shr     di,1                        ; scan line
        add     di,ax                       ;offset of first dest rect pixel in page
        add     di,[bp&#43;DestPageBase]    ;offset of first dest rect pixel
                                            ; in display memory
        and     cl,011b                        ;CL = first dest pixel&#146   ;s plane
        mov     al,11h                      ;upper nibble comes into play when plane wraps
                                            ; from 3 back to 0
        shl     al,cl                       ;set the bit for the first dest pixel&#146;s plane
        mov     [bp&#43;LeftMask],al        ; in each nibble to 1

        mov     ax,[bp&#43;SourceEndX]      ;calculate # of pixels across
        sub     ax,[bp&#43;SourceStartX]    ; rect
        jle     CopyDone                    ;skip if 0 or negative width
        mov     [bp&#43;RectWidth],ax
        sub     word ptr [bp&#43;SourceBitmapWidth],ax
                                            ;distance from end of one source scan line
                                            to start of next
        mov     ax,[bp&#43;SourceEndY]
        sub     ax,[bp&#43;SourceStartY]    ;height of rectangle
        jle     CopyDone                    ;skip if 0 or negative height
        mov     [bp&#43;RectHeight],ax
        mov     dx,SC_INDEX                 ;point to SC Index register
        mov     al,MAP_MASK
        out     dx,al                      ;point SC Index reg to the Map Mask
        inc     dx                         ;point DX to SC Data reg
CopyRowsLoop:
        mov     al,[bp&#43;LeftMask]
        mov     cx,[bp&#43;RectWidth]
        push    di                         ;remember the start offset in the dest
CopyScanLineLoop:
        cmp     byte ptr [bx],0            ;is this pixel mask-enabled?
        jz      MaskOff                    ;no, so don&#146;t draw it
                                           ;yes, draw the pixel
        out     dx,al                      ;set the plane for this pixel
        mov     ah,[si]                    ;get the pixel from the source
        mov     es:[di],ah                 ;copy the pixel to the screen
MaskOff:
        inc     bx                         ;advance the mask pointer
        inc     si                         ;advance the source pointer
        rol     al,1                       ;set mask for next pixel&#146;s plane
        adc     di,0                       ;advance destination address only when
                                           ;wrapping from plane 3 to plane 0
        loop    CopyScanLineLoop
        pop     di                         ;retrieve the dest start offset
        add     di,[bp&#43;DestBitmapWidth];point to the start of the
                                           ; next scan line of the dest
        add     si,[bp&#43;SourceBitmapWidth] ;point to the start of the
                                           ;next scan line of the source
        add     bx,[bp&#43;SourceBitmapWidth] ;point to the start of the
                                           ;next scan line of the mask
        dec     word ptr [bp&#43;RectHeight] ;count down scan lines
        jnz     CopyRowsLoop
CopyDone:
        pop     di                          ;restore caller&#146;s register variables
        pop     si
        mov     sp,bp                       ;discard storage for local variables
        pop     bp                          ;restore caller&#146;s stack frame
        ret
_CopySystemToScreenMaskedX endp
        end
</PRE>
<!-- END CODE //-->
<P><BR></P>
<CENTER>
<TABLE BORDER>
<TR>
<TD><A HREF="../ch48/48-05.html">Previous</A></TD>
<TD><A HREF="../index.html">Table of Contents</A></TD>
<TD><A HREF="49-02.html">Next</A></TD>
</TR>
</TABLE>
</CENTER>

<hr width="90%" size="1" noshade>
<div align="center">
<font face="Verdana,sans-serif" size="1">Graphics Programming Black Book &copy; 2001 Michael Abrash</font>
</div>
<!-- all of the reference materials (books) have the footer and subfoot reveresed -->
<!-- reference_subfoot = footer -->
<!-- reference_footer = subfoot -->

<!-- BEGIN SUB FOOTER -->
</BODY>
</HTML>

<!-- END FOOTER -->


